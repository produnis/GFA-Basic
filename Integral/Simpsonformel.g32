FullW # 1
'##B#
Mode StrSpace 0 : AutoRedraw = 1 : BackColor = colBtnFace : FontName = "fixedsys"
Global Double e, h, h0, x, xo, xu, y, y0, y1, y2 : Global Int32 i, k, n, n0, r, z, f$
e = 10 ^ -12 : r = 32 : Dim s(r) As Double
Print
Print "       Numerische Berechnung bestimmter Integrale von Funktionen y=f(x)"
Print "    mit der Simpsonformel, konvergenzbeschleunigt nach der Romberg-Methode"
Print "    **********************************************************************"
Print
Print " Wurde der Integrand y=f(x) bereits in den Programmcode eingesetzt?"
Input " Tippen Sie hier bitte ein 'j' für ja oder ein 'n' für nein ein! ";f$
Print
If f$ <> "j" And f$ <> "ja"
  Print " Bitte beenden Sie den Programmablauf jetzt durch Schließen dieses Fensters"
  Print " und geben Sie vor einem Neustart die zu integrierende Funktion y=f(x)"
  Print " in das Unterprogramm 'Sub INTEGRAND' ein"
  Do : Sleep : Until Me Is Nothing : CloseW # 1 : End
EndIf
Input " Untere Grenze des Integrals ? ";xu
Input " Obere  Grenze des Integrals ? ";xo
Print
Print " Ergebnis:"

h0 = xo - xu : x = xu : INTEGRAND
y0 = y : Add x, 0.5 * h0 : INTEGRAND
y1 = y : x = xo : INTEGRAND
Add y0, y : s(1) = h0 * (y0 + 4 * y1) / 6
For k = 2 To r
  Add y2, y1 : y1 = 0 : n = 2 ^ (k - 1) : h = h0 / n
  For i = 1 To n
    x = xu + h * (i - 0.5) : INTEGRAND
    Add y1, y
  Next i
  s(k) = h * (y0 + 4 * y1 + 2 * y2) / 6          'Simpsonformel
  z = 16
  For i = k - 1 DownTo 1                         'Romberg-Extrapolation
    s(i) = s(i + 1) + (s(i + 1) - s(i)) / (z - 1)
    If Abs(s(i + 1) - s(i)) < e
      Print " Integralwert = "; Round(s(i), 12)
      Print
      Print " ENDE  Durch Schließen dieses Fensters beenden Sie die Anwendung!"
      Do : Sleep : Until Me Is Nothing : CloseW # 1 : End
    Else If k = r
      Print " Konnte keine befriedigende Lösung finden! Eine Erhöhung des Wertes"
      Print " der Variablen e für die Fehlertoleranz ist eventuell zielführend."
      Print
      Print " ENDE  Durch Schließen dieses Fensters beenden Sie die Anwendung!"
      Do : Sleep : Until Me Is Nothing : CloseW # 1 : End
'##E#
    EndIf
    Mul z, 4
  Next i
Next k
Sub INTEGRAND                          'In diese Subroutine y=f(x) eingeben!
  y = Exp(-x * x / 2) / Sqr(2 * PI)
  Rem y = x * Exp(x) / (x + 1) ^ 2
  Rem y = 1 / x
Return
