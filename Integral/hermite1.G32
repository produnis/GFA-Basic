FullW # 1
Mode StrSpace 0 : AutoRedraw = 1 : BackColor = colBtnFace : FontName = "fixedsys"
Global Double a, b, e, g, gu, h, h0, x, xo, xu, y, yu : e = 10 ^ -12
Global Int32 f$, i, k, n, r, z : r = 32 : Dim s(r) As Double
Print
Print "                Numerische Berechnung bestimmter Integrale von Funktionen y=f(x)"
Print "             nach der Hermiteformel, konvergenzbeschleunigt nach der Romberg-Methode"
Print " (Die Berechnung der Ableitungswerte an den Intervallgrenzen erfordert die Eingabe von g=f'(x))"
Print " **********************************************************************************************"
Print
Print " Wurden der Integrand y=f(x) und dessen Ableitung g=f'(x) bereits in den Programmcode eingesetzt?"
Input " Tippen Sie hier bitte ein 'j' für ja oder ein 'n' für nein ein ", f$
Print
If f$ <> "j" And f$ <> "ja"
  Print " Bitte beenden Sie den Programmablauf jetzt durch Schließen dieses Fensters"
  Print " und geben Sie vor einem Neustart die zu integrierende Funktion y=f(x) in"
  Print " das Unterprogramm 'Sub INTEGRAND' und die Ableitung g=f'(x) in das"
  Print " Unterprogramm 'Sub ABLEITUNG' ein."
  Do : Sleep : Until Me Is Nothing : CloseW # 1 : End
EndIf
Input " Untere Grenze des Integrals ? ";xu
Input " Obere  Grenze des Integrals ? ";xo
Print
Print " Ergebnis:"

h0 = xo - xu : x = xu : INTEGRAND
yu = y : ABLEITUNG
gu = g : x = xo : INTEGRAND
a = (yu + y) / 2 : ABLEITUNG
b = (gu - g) / 12 : s(1) = h0 * (a + b * h0)
For k = 2 To r
  n = 2 ^ (k - 2) : h = h0 / (2 * n)
  For i = 1 To n
    x = xu + h * (2 * i - 1) : INTEGRAND
    Add a, y
  Next i
  s(k) = h * (a + b * h)
  z = 16
  For i = k - 1 DownTo 1
    s(i) = (s(i + 1) * z - s(i)) / (z - 1)             'Romberg-Extrapolation
    If Abs(s(i + 1) - s(i)) < e
      Print " Integralwert = "; Round(s(i), 12)
      Print
      Print " ENDE  Durch Schließen dieses Fensters beenden Sie die Anwendung!"
      Do : Sleep : Until Me Is Nothing : CloseW # 1 : End
    Else If k = r
'##E#
      Print " Es konnte keine Lösung gefunden werden! Eine Erhöhung des Wertes"
      Print " der Variablen e für die Fehlertoleranz ist eventuell zielführend."
      Print
      Print " ENDE  Durch Schließen dieses Fensters beenden Sie die Anwendung!"
      Do : Sleep : Until Me Is Nothing : CloseW # 1 : End
    EndIf
    Mul z, 4
  Next i
Next k

Sub INTEGRAND                            'In diese Subroutine y=f(x) eingeben!
  y = Exp(-x ^ 2 / 2) / Sqr(2 * PI)
  Rem y = 1 / x
Return

Sub ABLEITUNG                            'In diese Subroutine g=f'(x) eingeben!
  g = -x * Exp(-x ^ 2 / 2) / Sqr(2 * PI)
  Rem g = -1 / x ^ 2
Return
