FullW # 1
Mode StrSpace 0 : AutoRedraw = 1 : BackColor = colBtnFace : FontName = "fixedsys"
Global Double c : Global Long i, j, k, n, r : Global d$
Print
Print "    Lösung von Systemen linearer Gleichungen nach Gauß-Banachievicz"
Print "                        mit Zeilenpivotisierung"
Print "    ***************************************************************"
Print
Input " Wie viele Gleichungen bzw. Unbekannte 2<=n<=10 hat das zu lösende System? ", n
While n < 2 Or n > 10
  Print " Achtung! Für n ist eine ganze positive Zahl >=2 und <=10 einzugeben!"
  Input " n = ";n
Wend
Dim a(n, n), b(n) As Double : Dim t(n) As Long
Cls : Print
Print " Die allgemeine Form des Gleichungssystems ist:"
If n = 2
  Print " a(1,1)*x1+a(1,2)*x2 = b(1)"
  Print " a(2,1)*x1+a(2,2)*x2 = b(2)"
Else If n = 3
  Print " a(1,1)*x1+a(1,2)*x2+a(1,3)*x3 = b(1)"
  Print " a(2,1)*x1+a(2,2)*x2+a(2,3)*x3 = b(2)"
  Print " a(3,1)*x1+a(3,2)*x2+a(3,3)*x3 = b(3)"
Else If n > 3 And n < 10
  d$ = "+.......+"
  Print " a(1,1)*x1+a(1,2)*x2"; d$; "a(1,"; n; ")*x"; n; " = b(1)"
  Print "    :         :                 :          :"
  Print " a("; n; ",1)*x1+a("; n; ",2)*x2"; d$; "a("; n; ","; n; ")*x"; n; " = b("; n; ")"
Else If n = 10
  Print " a(1,1)*x1+a(1,2)*x2"; "+......+"; "a(1,"; n; ")*x"; n; " = b(1)"
  Print "    :         :                 :           :"
  Print " a("; n; ",1)*x1+a("; n; ",2)*x2"; "+...+"; "a("; n; ","; n; ")*x"; n; " = b("; n; ")"
EndIf
Print
Print " Bitte weisen Sie den Koeffizienten a(1,1) bis a("; n; ","; n; ")"
Print " und den Größen b(1) bis b("; n; ") relevante Zahlenwerte zu"

For i = 1 To n
  t(i) = i
  For j = 1 To n
    Print " a("; i; ","; j; ") ="
    Locate 11, CrsLin - 1
    Input a(i, j)
  Next j
  Print " b("; i; ")   ="
  Locate 11, CrsLin - 1
  Input b(i)
  Print
Next i
Print " Ergebnis:"

For k = 1 To n - 1
  c = Abs(a(k, k))
  r = k
  For i = k + 1 To n
    If c < Abs(a(k, i)) Then c = Abs(a(k, i)) : r = i
  Next i
  If r <> k Then Swap t(k), t(r)
  For i = 1 To n
    Swap a(i, k), a(i, r)
  Next i
  If Abs(a(k, k)) == 0 Then Print; " Abbruch wegen Division durch Null!" : ENDE

  For i = k + 1 To n
    For j = 1 To k - 1
      Sub a(i, k), a(i, j) * a(j, k)
    Next j
    Div a(i, k), a(k, k)
  Next i
  For i = k + 1 To n
    For j = 1 To k
      Sub a(k + 1, i), a(k + 1, j) * a(j, i)
    Next j
  Next i
Next k
If Abs(a(n, n)) == 0 Then Print " Abbruch wegen Division durch Null!" : ENDE

For i = 2 To n
  For j = 1 To i - 1
    Sub b(i), b(j) * a(i, j)
  Next j
Next i
Div b(n), a(n, n)
For i = n - 1 DownTo 1
  For j = i + 1 To n
    Sub b(i), a(i, j) * b(j)
  Next j
  Div b(i), a(i, i)
Next i

For i = 1 To n
  j = i
  While i <> t(j) : j = t(j) : Wend
  Print " x"; i; " = "; Round(b(j), 6)
Next i
ENDE
Sub ENDE
  Print : Print " ENDE   Durch Schließen dieses Fensters beenden Sie die Anwendung!"
  Do : Sleep : Until Me Is Nothing : CloseW # 1 : End
