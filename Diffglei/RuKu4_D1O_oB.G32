FullW # 1
Mode StrSpace 0 : AutoRedraw = 1 : BackColor = colBtnFace : FontName = "fixedsys"
Global Int32 i, j, n, t : Global Double h, h0, k1, k2, k3, x, x0, xa, xe, y, y1 : Global f$
Print
Print "  Lösung von Anfangswertaufgaben gewöhnlicher  Differentialgleichungen 1. Ordnung"
Print "  nach einem Algorithmus 4. Ordnung von Runge/Kutta ohne Konvergenzbeschleunigung"
Print "  *******************************************************************************"
Print
Print " Haben Sie die Anleitungsinformation zu diesem Programm bereits gelesen und die zu"
Print " lösende Differentialgleichung in den Quellcode eingegeben?"
Input " Quittieren Sie diese Frage durch Eingabe eines 'j' für ja oder eines 'n' für nein! ";f$

If f$ <> "j" And f$ <> "ja"
  Cls
  Print " Anleitung für die Eingabe der zu lösenden DG in den Qellcode dieses Programms"
  Print " *****************************************************************************"
  Print
  Print " Die zu lösende Differentialgleichung darf nicht von höherer als erster Ordnung"
  Print " sein und muss für die Einbindung in den Quellcode dieses Programms in die Form"
  Print
  Print " y1 = f(x,y)"
  Print
  Print " entwickelt werden. Darin bezeichnen: y die gesuchte Stammfunktion, y1 deren"
  Print " erste Ableitung und x die unabhängige Variable."
  Print
  Print " ANMERKUNG: Im einfachsten Fall hat eine DG 1. Ordnung die Form:  y1 = f(x),"
  Print " wobei f(x) auch eine Konstante sein kann. Die Stammfunktion y ergibt sich dann"
  Print " einfach als Integral von f(x) plus Integrationskonstante."
  Print
  Print " Geben Sie nun bitte nach Schließen dieses Fensters die aktuell zu lösende DG"
  Print " in der vorgeschriebenen Form in das Unterprogramm DIFFGLEI des Quellcodes ein"
  Print " und starten Sie danach die Anwendung neu!"
  Do : Sleep : Until Me Is Nothing : CloseW # 1 : End
EndIf
Print

Input " Mit welchem Wert von x soll die Berechnung starten ? ", xa
Input " Wie groß ist der zugehörige Anfangswert von y      ? ", y
Input " Bei welchem Wert von x soll die Berechnung enden   ? ", xe
Input " Für welche Delta-x wird Ergebnisausgabe gewünscht  ? ", h0
Print " Wie viele Rechenzyklen sollen auf jeden Schritt"
Input " Delta-x für die Werteausgabe entfallen (z.B. 100)  ? ", n
Cls
Print " Ergebnis: Die Stammfunktion y(x) und deren 1. Ableitung y1=y'(x)"
Print " haben im vorgegebenen Intervall folgenden numerischen Verlauf:"
Print

x = xa
diffglei
Print Tab(8); "x="; Tab(27); "y(x)="; Tab(51); "y1=y'(x)="
'##E#
Print Format(x, " * #####0.00"), Format(y, "   * ########0.00000000"), Format(y1, "* ########0.00000000")
h = h0 / (2 * n) : t = (xe - xa) / h0
For j = 0 To t - 1
  x0 = xa + j * h0
  For i = 1 To n
    diffglei
    k1 = y1 * h : Add y, k1 : Add x, h
    diffglei
    k2 = y1 * h : Add y, k2 - k1
    diffglei
    k3 = 2 * y1 * h : Add y, k3 - k2 : x = x0 + 2 * i * h
    diffglei
    Add y, (y1 * h + k1 + 2 * (k2 - k3)) / 3
  Next i
  diffglei
  Print Format(x, " * #####0.00"), Format(y, "   * ########0.00000000"), Format(y1, "* ########0.00000000")
Next j
Print
Print " ENDE   Durch Schließen dieses Fensters wird die Anwendung beendet!"
Do : Sleep : Until Me Is Nothing : CloseW # 1 : End

Sub diffglei                        'In diese Subroutine die D.-Gleichung y1=f(x,y) eingeben!
  Rem y1 = x + y
  y1 = x ^ 2 + 2 * x - 8
Return
